group = artifact_group
version = artifact_version

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.jetbrains.dokka-android'

dokka {
    outputFormat = 'javadoc'
    noStdlibLink = true
    noAndroidSdkLink = true
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

task javadocJar(type: Jar, dependsOn: dokka) {
    from "$buildDir/dokka"
    archiveClassifier.set('javadoc')
}

/**
 * for Deploy test.
 * Maven deploy settings.
 * https://developer.android.com/studio/build/maven-publish-plugin
 */
afterEvaluate {
    publishing {
        repositories {
            maven {
                url "file://${System.getProperty('user.home')}/.m2/repository"
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                // Adds javadocs and sources as separate jars.
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

bintray {
    user = 'eaglesakura'
    key = System.env.BINTRAY_API_KEY
    publish = true
    override = true
    pkg {
        repo = "maven"
        name = artifact_name
        vcsUrl = bintray_vcs_url
        issueTrackerUrl = "${bintray_vcs_url}/issues"
        licenses = ["MIT"]
        labels = bintray_labels
        publications = ['release']
        version {
            name = project.version
            released = new Date()
            gpg {
                sign = true
                passphrase = System.env.BINTRAY_GPG_PASS
            }
        }
    }
    configurations = ['archives']
}

println ":${project.name}:bintrayUpload  -> ${artifact_group} : ${version}"
println ":${project.name}:uploadArchives -> file:${System.getProperty('user.home')}/.m2/repository : ${version}"
